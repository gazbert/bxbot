buildscript {

    repositories {
        jcenter()
    }

    dependencies {
        classpath("io.spring.gradle:dependency-management-plugin:0.6.0.RELEASE")

        // v 1.4 Broken - does not include my classes in the uber jar :-(
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.3.6.RELEASE")
    }
}

plugins {
    id 'com.gradle.build-scan' version '1.2'
}

buildScan {
    licenseAgreementUrl = 'https://gradle.com/terms-of-service'
    licenseAgree = 'yes'
}

ext.versions = [
        springBootVersion:'1.4.0.RELEASE',
        springCoreVersion:'4.3.2.RELEASE'
]

ext.libraries = [
        spring_boot_starter: dependencies.create("org.springframework.boot:spring-boot-starter:" + ext.versions.springBootVersion) {
            exclude module: "spring-boot-starter-logging"
            force = true
        },
        spring_boot_starter_log4j2: dependencies.create("org.springframework.boot:spring-boot-starter-log4j2:" + ext.versions.springBootVersion),
        spring_boot_starter_web: dependencies.create("org.springframework.boot:spring-boot-starter-web:" + ext.versions.springBootVersion) {
            exclude module: "logback-classic"
            force = true
        },
        spring_boot_starter_actuator: dependencies.create("org.springframework.boot:spring-boot-starter-actuator:" + ext.versions.springBootVersion) {
            exclude module: "logback-classic"
            force = true
        },
        spring_boot_starter_security: dependencies.create("org.springframework.boot:spring-boot-starter-security:" + ext.versions.springBootVersion) {
            exclude module: "logback-classic"
            force = true
        },
        spring_boot_starter_data_jpa: dependencies.create("org.springframework.boot:spring-boot-starter-data-jpa:" + ext.versions.springBootVersion) {
            exclude module: "logback-classic"
            force = true
        },
        spring_core: dependencies.create("org.springframework:spring-core:" + ext.versions.springCoreVersion),
        spring_tx: dependencies.create("org.springframework:spring-tx:" + ext.versions.springCoreVersion),
        spring_security_oauth2: dependencies.create("org.springframework.security.oauth:spring-security-oauth2:2.0.11.RELEASE"),
        google_guava: dependencies.create("com.google.guava:guava:19.0"),
        google_gson: dependencies.create("com.google.code.gson:gson:2.7"),
        javax_mail_api: dependencies.create("javax.mail:javax.mail-api:1.5.5"),
        javax_mail_sun: dependencies.create("com.sun.mail:javax.mail:1.5.5"),
        hsqldb: dependencies.create("org.hsqldb:hsqldb:2.3.4"),

        junit: dependencies.create("junit:junit:4.12"),
        powermock_junit: dependencies.create("org.powermock:powermock-module-junit4:1.6.2"),
        powermock_api_easymock: dependencies.create("org.powermock:powermock-api-easymock:1.6.2"),
        easymock: dependencies.create("org.easymock:easymock:3.3.1") {
            exclude module: "cglib-nodep"
            force = true
        },
        objenesis: dependencies.create("org.objenesis:objenesis:2.1"),
        cglib_nodep: dependencies.create("cglib:cglib-nodep:2.2.2"),
        spring_boot_starter_test: dependencies.create("org.springframework.boot:spring-boot-starter-test:" + ext.versions.springBootVersion)
]

allprojects {

    apply plugin: "io.spring.dependency-management"
    apply plugin: 'maven'

    group = 'com.gazbert.bxbot'
        version = '0.5-beta.2-SNAPSHOT'
}

subprojects {

    apply plugin: 'java'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenLocal()
        maven { url "http://repo.maven.apache.org/maven2" }
    }

    test {
        testLogging {
            events "passed", "skipped", "failed"
        }
    }

    javadoc {
        include "**/api/**"
    }
}

// TODO strip out common stuff into separate method?
task buildZipDist(type: Zip, dependsOn: build) {

    baseName = 'bxbot-app'

    into("$baseName-$version") {

        into('libs') {
            from 'bxbot-app/build/libs'
            include '*.jar' //copy the fat jar created by bootRepackage
        }

        into('') {
            from 'README.md'
        }

        into('') {
            from 'CONTRIBUTING.md'
        }

        into('') {
            from 'LICENSE'
        }

        into('') {
            from 'third_party_licenses.txt'
        }

        into('') {
            from 'MAINTAINERS.txt'
        }

        into('') {
            from 'bxbot.sh'
        }

        from('config/application.properties') {
            into 'config'
        }

        into('config') {
            from 'config'
            include('*.xml')
        }
    }
}

// TODO strip out common stuff into separate method?
task buildTarGzipDist(type: Tar, dependsOn: build) {

    baseName = 'bxbot-app'
    extension = 'tar.gz'
    compression = Compression.GZIP

    into("$baseName-$version") {

        into('libs') {
            from 'bxbot-app/build/libs'
            include '*.jar' //copy the fat jar created by bootRepackage
        }

        into('') {
            from 'README.md'
        }

        into('') {
            from 'CONTRIBUTING.md'
        }

        into('') {
            from 'LICENSE'
        }

        into('') {
            from 'third_party_licenses.txt'
        }

        into('') {
            from 'MAINTAINERS.txt'
        }

        into('') {
            from 'bxbot.sh'
        }

        from('config/application.properties') {
            into 'config'
        }

        into('config') {
            from 'config'
            include('*.xml')
        }
    }

    task wrapper(type: Wrapper) {
        gradleVersion = '3.2.1'
    }
}