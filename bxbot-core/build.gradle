description = 'BX-bot Core'

dependencies {

  implementation project(':bxbot-domain-objects')
  implementation project(':bxbot-services')
  implementation project(':bxbot-trading-api')
  implementation project(':bxbot-exchange-api')
  implementation project(':bxbot-strategy-api')
  implementation project(':bxbot-exchanges')
  implementation project(':bxbot-strategies')

  implementation libraries.spring_boot_starter
  implementation libraries.spring_boot_starter_log4j2
  implementation libraries.google_guava

  implementation libraries.jakarta_mail_api
  implementation libraries.jakarta_mail_sun

  implementation libraries.springdoc_openapi_ui

  testImplementation libraries.powermock_junit
  testImplementation libraries.powermock_api_easymock
  testImplementation libraries.easymock
  testImplementation libraries.junit_vintage_engine
  testImplementation libraries.spring_boot_starter_test
  testImplementation libraries.awaitility
}

// For Powermock tests messing with JDK 17 bytecode.
// See: https://stackoverflow.com/questions/69896191/powermock-compatibility-with-jdk-17
test.jvmArgs "--add-opens", "java.base/java.lang=ALL-UNNAMED"
test.jvmArgs "--add-opens", "java.base/java.util=ALL-UNNAMED"
test.jvmArgs "--add-opens", "java.base/java.util.concurrent=ALL-UNNAMED"
test.jvmArgs "--add-opens", "java.base/java.text=ALL-UNNAMED"

jacocoTestCoverageVerification {
  violationRules {
    rule {
      element = 'BUNDLE'
      // Ignore classes tested using Powermock. JaCoCo won't/can't instrument them:
      // "... right now there is NO WAY TO USE PowerMock with JaCoCo On-the-fly instrumentation."
      // See: https://github.com/powermock/powermock/wiki/Code-coverage-with-JaCoCo
      excludes = [
          'com.gazbert.bxbot.core.mail.EmailAlerter*', // Powermock tests
          'com.gazbert.bxbot.BxBot',                   // Nothing to test
      ]
      limit {
        counter = 'LINE'
        value = 'COVEREDRATIO'
        minimum = 0.7
      }
    }
  }
}
